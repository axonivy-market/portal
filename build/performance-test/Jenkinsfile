pipeline {
  agent { label 'linux' }

  options {
    buildDiscarder(logRotator(numToKeepStr: '200', artifactNumToKeepStr: '5'))
  }

  triggers {
    cron('0 14 * * *')
  }

  environment {
    engineVersion = '10'
    devEngineDir = "/var/tools/ivy/${env.engineVersion}"
    performanceEngineDir = "/var/tools/ivy/performance-${env.engineVersion}"
    masterEngineDir = "/var/tools/ivy/master"
    performanceMasterEngineDir = "/var/tools/ivy/performance-master"
    portalApplicationName = 'Portal'
    jmeterSourceDir = 'AxonIvyPortal/PortalTest/jmeter'
  }

  stages {
    stage('deploy') {
      steps {
        script {
          sh """
            docker compose -f ${masterEngineDir}/docker-compose.yml down
            docker compose -f ${performanceMasterEngineDir}/docker-compose.yml down
            docker compose -f ${devEngineDir}/docker-compose.yml down
            docker compose -f ${performanceEngineDir}/docker-compose.yml down
            docker compose -f ${performanceEngineDir}/docker-compose.yml up -d
          """

          docker.build('build-portal-10-modules', '-f build/Dockerfile .').inside('-v /var/tools/maven-cache:/home/build/') {
            def modules = ['AxonIvyPortal/portal-components', 'AxonIvyPortal/portal']
            def buildPluginVersion = readMavenPom(file: 'AxonIvyPortal/portal-components/pom.xml').getProperties().getProperty('build.plugin.version')
            def engineUrl = 'http://10.123.1.216:9010'
            for (module in modules) {
              maven cmd: "clean install -f ${module}/pom.xml"
              maven cmd: "com.axonivy.ivy.ci:project-build-plugin:${buildPluginVersion}:deploy-to-engine -f ${module}/pom.xml -Divy.deploy.server.id=engine-cockpit -Divy.deploy.engine.app=Portal -Divy.deploy.method=HTTP -Divy.deploy.engine.url=${engineUrl} -Divy.test.engine=MODIFY_EXISTING"
            }
          }
        }
      }
    }
    stage('test') {
      steps {
        script {
          def today = new Date().format('dd-MM-yyyy_HH-mm')
          resultDir = "/var/tools/performance-result/${env.engineVersion}/${today}"
          fileOperations([folderCreateOperation(folderPath: "${resultDir}")])

          echo '====================Test one admin user===================='
          updateConfigFile('data/single_admin_user_server.csv', '1')
          // First executing takes more time, so not record the result of first time
          executeJMeter('../tmp.jtl')

          executeJMeter('result_1_admin_user.jtl')

          echo '====================Test for one normal user===================='
          updateConfigFile('data/single_normal_user_server.csv', '1')
          executeJMeter('result_1_normal_user.jtl')

          echo '====================Test ten users===================='
          updateConfigFile('data/users_server.csv', '10')
          executeJMeter('result_10_users.jtl')
        }
      }
    }
  }

  post {
    always {
      script {
        sh """
          docker compose -f ${performanceEngineDir}/docker-compose.yml down
          docker compose -f ${devEngineDir}/docker-compose.yml up -d
          docker compose -f ${masterEngineDir}/docker-compose.yml up -d
        """
      }
      script {
        if (binding.hasVariable('today')) {
          perfReport sourceDataFiles: "${resultDir}/*.jtl", compareBuildPrevious :true, ignoreFailedBuilds: true, ignoreUnstableBuilds: true, errorFailedThreshold: 1
        }
      }
    }
  }
}

def updateConfigFile(acccountFilePath, numberOfUser) {
  sh """
    awk -F'=' -v OFS='=' -v newval='${acccountFilePath}' '/^file.csv/{\$2=newval;print;next}1' ${jmeterSourceDir}/test.properties > ${jmeterSourceDir}/tmp.properties
    awk -F'=' -v OFS='=' -v newval='${numberOfUser}' '/^portal.thread.numberOfUser/{\$2=newval;print;next}1' ${jmeterSourceDir}/tmp.properties > ${jmeterSourceDir}/test.properties
  """
}

def executeJMeter(resultFileName) {
  def binDir = '/opt/apache-jmeter-5.5/bin'
  docker.build('jmeter-performance-10-test', '-f build/performance-test/Dockerfile .').inside("""
        -v ${WORKSPACE}/${jmeterSourceDir}:${binDir}/portal-jmeter-10
        -v ${resultDir}:${binDir}/result
      """) {
    sh "${binDir}/jmeter -Jjmeter.save.saveservice.subresults=false -n -t ${binDir}/portal-jmeter-10/portal_walkthrough_testplan.jmx -l ${binDir}/result/${resultFileName}"
  }
}