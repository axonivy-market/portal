pipeline {
  agent {
    label 'portal-slave-linux'
  }

  options {
    buildDiscarder(logRotator(numToKeepStr: '60', artifactNumToKeepStr: '60'))
  }

  triggers {
    cron('0 16 * * *')
  }

  parameters {
    string(name: 'testPatternToRun', defaultValue: 'com.axonivy.portal.selenium.test.**.*Test', description: 'Test pattern to run')
    string(name: 'engineDownloadURL', defaultValue: '-Divy.engine.download.url=https://developer.axonivy.com/permalink/nightly-10/axonivy-engine.zip', description: '''Where to download engine? e.g.
      -Divy.engine.download.url=https://developer.axonivy.com/permalink/nightly-10/axonivy-engine.zip for nightly 10
      -Divy.engine.download.url=file:///home/build/engine/AxonIvyEngineX.X.X.X_Linux_x64.zip for downloaded engine
      -Divy.engine.list.url=https://jenkins.ivyteam.io/job/core_product/job/release%252F10.0/lastSuccessfulBuild/artifact/workspace/ch.ivyteam.ivy.server.product/target/products/ -Divy.engine.os.arch=Windows_x64 for last successful build 10.0
    ''')
  }

  environment {
    engineDir = "${env.WORKSPACE}/AxonIvyPortal/portal-selenium-test/engine"
  }

  stages {
    stage('build') {
      steps {
        script {
          currentBuild.description = "On ${env.NODE_NAME}"
          def random = (new Random()).nextInt(10000000)
          def networkName = "build-" + random
          def seleniumName = "selenium-" + random
          def testEngineContainerName = "wawa-" + random
          def debugMode = false
          def debugPort = debugMode ? '-p 7900:7900 -e SE_VNC_NO_PASSWORD=1' : '-e SE_START_VNC=false'
          def headlessConfig = debugMode ? '-Divy.selenide.headless=false' : ''
          sh "docker network create ${networkName}"
          try {
            docker.image("selenium/standalone-firefox:4").withRun("--shm-size=2g --name ${seleniumName} --network ${networkName} --hostname=wawa ${debugPort} -e SE_NODE_MAX_SESSIONS=2") {
              docker.build('gui-test-portal', '-f build/Dockerfile .').inside("--name ${testEngineContainerName} --network ${networkName} -v /var/tools/maven-cache:/home/build/") {
                def utils = load 'build/common/utils.groovy'
                utils.init()
                utils.extractEngine(env.engineDir, params.engineDownloadURL)

                echo '====================Build Portal modules===================='
                def modules = ['AxonIvyPortal/portal-components', 'AxonIvyPortal/portal', 'AxonIvyPortal/AxonIvyExpress', 'AxonIvyPortal/PortalKitTestHelper', 'Showcase/portal-user-examples', 'Showcase/portal-developer-examples', 'Showcase/InternalSupport', 'Showcase/portal-components-examples', 'AxonIvyPortal/PortalApp', 'Showcase/portal-demo-app']
                for (module in modules) {
                  maven cmd: "clean install -ntp -f ${module}/pom.xml -Divy.engine.directory=${env.engineDir}"
                }

                echo '====================Deploy Portal modules===================='
                sh """
                  DEPLOYMENT="${env.engineDir}/system/demo-applications/demo-portal"
                  rm -rf \${DEPLOYMENT}/*
                  cp Showcase/portal-demo-app/target/*.zip \${DEPLOYMENT}/
                  cp Showcase/portal-developer-examples/target/*.iar \${DEPLOYMENT}/
                  cp Showcase/portal-components-examples/target/*.iar \${DEPLOYMENT}/
                """

                echo '====================Execute maven for testing===================='
                maven cmd: "clean verify -ntp" +
                  " -f AxonIvyPortal/portal-selenium-test/customized_pom.xml" +
                  " ${params.engineDownloadURL}" +
                  " -Dtest=${params.testPatternToRun}" +
                  " -DbrowserType=FIREFOX" +
                  " -Dcapabilities.unhandledPromptBehavior=accept" +
                  " -DtrimStackTrace=false" +
                  " -Divy.engine.directory=${env.engineDir}" +
                  " -Divy.deploy.timeout.seconds=60" +
                  " -Divy.engine.start.timeout.seconds=120" +
                  " -Dsurefire.rerunFailingTestsCount=2" +
                  " -Dselenide.remote=http://${seleniumName}:4444/wd/hub" +
                  " -Dtest.engine.url=http://${testEngineContainerName}:8080" +
                  " ${headlessConfig}"
              }
            }
          } finally {
            sh "docker network rm ${networkName}"
          }

          echo '====================Archive consoleText ===================='
          withCredentials([usernameColonPassword(credentialsId: 'wawa-jenkins', variable: 'credentials')]) {
            def branchName = env.BRANCH_NAME.replaceAll("/", "%2F")
            sh """
              curl --user ${credentials} ${env.JENKINS_URL}job/portal-selenium-test/job/${branchName}/${env.BUILD_NUMBER}/consoleText --output logText.txt
              cp AxonIvyPortal/portal-selenium-test/resources/extractFlakyTests.sh .
              chmod +x extractFlakyTests.sh
              ./extractFlakyTests.sh
            """
          }
        }
      }
    }
  }

  post {
    always {
      archiveArtifacts artifacts: 'AxonIvyPortal/portal-selenium-test/target/selenide/reports/**/*.*, AxonIvyPortal/portal-selenium-test/target/testEngineOut.log, testReport.txt, logText.txt', allowEmptyArchive: true
      junit allowEmptyResults: true, testResults: '**/surefire-reports/TEST-*.xml'
    }
  }
}