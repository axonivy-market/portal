pipeline {
  agent {
    label 'linux'
  }

  options {
    buildDiscarder(logRotator(numToKeepStr: '60', artifactNumToKeepStr: '5'))
  }

  parameters {
    choice(name: 'engine', choices: ['portal','performanceTest'], description: 'Which engine to be upgraded?')
  }

  stages {
    stage('backupDatabase') {
      steps {
        script {
          currentBuild.description = "On ${env.NODE_NAME}"
          def today = new Date().format('dd-MM-yyyy_HH-mm')
          def config = [portal:[engine: '10', systemDatabase: 'AxonIvySystemDatabase-10'],
                    performanceTest:[engine: 'performance-10', systemDatabase: 'AxonIvySystemDatabase-performance-10']]
          def engineParentDir = '/var/tools/ivy/'
          engineDir = engineParentDir + config[params.engine]['engine']
          def systemDatabaseName = config[params.engine]['systemDatabase']
          def databaseBackupDir = '/var/database-backups/'
          def dbBackupFile = databaseBackupDir + systemDatabaseName + '-' + today + '.dump'

          // Set SystemDb Autoconvert: true - migrate System DB automatically when upgrading ivy
          updateConfigFile(' true')

          if (params.engine == 'performanceTest') {
            sh """
              echo ====================Start engine performance test====================
              docker compose -f ${engineDir}/docker-compose.yml up -d
            """
          }

          sh """
            echo ====================Backup database====================
            [ -d ${databaseBackupDir} ] ||  mkdir -p ${databaseBackupDir}
            docker exec -u postgres postgres pg_dump -Fc ${systemDatabaseName} > ${dbBackupFile}
          """
        }
      }
    }

    stage('upgradeEngine') {
      steps {
        script {
          if (params.engine == 'performanceTest') {
            sh """
              cd ${engineDir}
              echo ====================Stop engine performance test====================
              docker compose down
              docker compose pull
            """
          } else {
            sh """
              echo ====================Upgrade engine====================
              cd ${engineDir}
              docker compose down
              docker compose pull
              docker compose up -d
              # Wait engine starting so that Autoconvert: true takes affect
              sleep 100
            """
          }

          // Set SystemDb Autoconvert: false - avoid migrating SystemDB automatically when upgrading ivy manually
          updateConfigFile(' false')
        }
      }
    }
  }
}

def updateConfigFile(value) {
  sh """
    awk -F':' -v OFS=':' -v newval='${value}' '/^  Autoconvert:/{\$2=newval;print;next}1' ${engineDir}/ivy.yaml > ${engineDir}/tmp.yaml
    awk -F':' -v OFS=':' -v newval='${value}' '/^  Autoconvert:/{\$2=newval;print;next}1' ${engineDir}/tmp.yaml > ${engineDir}/ivy.yaml
  """
}

